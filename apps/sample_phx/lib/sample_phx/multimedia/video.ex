defmodule SamplePhx.Multimedia.Video do
  use Ecto.Schema
  import Ecto.Changeset

  # We can customize the primary key with the `@primary_key` module attributes.
  @primary_key {:id, SamplePhx.Multimedia.Permalink, autogenerate: true}
  schema "videos" do
    field :description, :string
    field :title, :string
    field :url, :string
    field :slug, :string

    belongs_to :user, SamplePhx.Accounts.User
    belongs_to :category, SamplePhx.Multimedia.Category
    has_many :annotations, SamplePhx.Multimedia.Annotation

    timestamps()
  end

  @doc false
  def changeset(video, attrs) do
    video
    |> cast(attrs, [:url, :title, :description, :category_id])
    |> validate_required([:url, :title, :description])
    |> assoc_constraint(:category)
    |> slugify_title()
  end

  defp slugify_title(changeset) do
    case fetch_change(changeset, :title) do
      {:ok, new_title} -> put_change(changeset, :slug, slugify(new_title))
      :error -> changeset
    end
  end

  defp slugify(str) do
    str
    |> String.downcase()
    |> String.replace(~r/[^\w-]+/, "-")
  end
end
